# Print this list
default:
    just --list

cargo-hack := `which cargo-hack` # Requires cargo-hack to be installed

# Format code
[arg("check", long="check", value="true")]
fmt check="":
    cargo fmt --all {{ if check == "" { "" } else { "-- --check" } }}

# Run clippy
[arg("fix", long="fix", value="true")]
[arg("force", long="force", value="true")]
clippy fix="" force="":
    cargo hack clippy --all-targets --feature-powerset --workspace \
        {{ if fix == "" { "" } else { if force == "" { "--fix" } else { "--fix --allow-dirty" } } }}

alias c := clippy
alias lint := clippy

# Run tests
test:
    cargo hack test --all-targets --feature-powerset --workspace
    # cargo hack test --doc --all-features --workspace

alias t := test

# Build docs
[arg("no_deps", long="no-deps", value="true")]
[arg("private", long="private", value="true")]
[arg("open", long="open", value="true")]
doc no_deps="" private="" open="":
    # use stable for now
    RUSTDOCFLAGS="${RUSTDOCFLAGS:-} -Zunstable-options --default-theme=ayu" \
    cargo hack doc --workspace --feature-powerset \
        {{ if no_deps == "" { "" } else { "--no-deps" } }} \
        {{ if private == "" { "" } else { "--document-private-items" } }} \
        {{ if open == "" { "" } else { "--open" } }}

# Build docs for all features and open in default browser
doco: (doc "--no-deps" "" "--open")

# Run all checks (~local CI)
ci: fmt clippy test doc
    @echo "All checks passed! :)"
